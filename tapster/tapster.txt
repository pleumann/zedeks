#program tapster
  10 ; 
  20 ; === Setup ===============
  30 ; 
  35 OUT 4667,1
  40 RUN AT 3  
  50 BORDER 7: PAPER 7: INK 0: BRIGHT 0: FLASH 0: INVERSE 0
  60 CLEAR 42767
  70 PROC Pokes() TO start,max,tape,check
  80 PROC Window(1,1,6,30,"Tapster")  
  90 PROC Window(10,1,21,30,"0K in 0 file(s)")
 100 CLOSE # 3: OPEN # 3,"w>11,2,10,28,4": LPRINT ; CHR$ 19; CHR$ 1; CHR$ 1;
 110 LPRINT '"Tapster Version 0.7"
 120 LPRINT "(c) 2020 Joerg Pleumann"
 130 LPRINT 
 140 LPRINT max;" bytes available"
 150 LPRINT 
 160 PRINT AT 2,2; BRIGHT 1;"Start"," [X]Keep dupes";
 170 PRINT AT 3,2; BRIGHT 1;"Directory"," [X]Fix names";
 180 PRINT AT 4,2; BRIGHT 1;"Help"," [X]Add suffix";
 190 PRINT AT 5,2; BRIGHT 1;"Quit"," [X]Headerless";
 200 DIM o(4): LET o(1)=1: LET o(2)=1: LET o(3)=1: LET o(4)=1
 210 LET mrow=0: LET mcol=0: LET mrow2=0: LET mcol2=0
 220 LPRINT "Directory is ";: PWD #3
 230 PROC FlashLight(0)
 240 ; 
 250 ; === Menu navigation ===
 260 ; 
 270 POKE 22528+65+mrow*32+mcol*15,"xxxxxxxxxxxxxxx"
 280 POKE 22528+65+mrow2*32+mcol2*15,"hhhhhhhhhhhhhhh"
 290 LET mrow=mrow2: LET mcol=mcol2
 300 PAUSE 0: LET k$= INKEY$ : BEEP 0.015,-20
 310 IF k$= CHR$ 8 AND mcol>0 THEN mcol2=0: GO TO %270
 320 IF k$= CHR$ 9 AND mcol<1 THEN mcol2=1: GO TO %270
 330 IF k$= CHR$ 10 AND mrow<3 THEN mrow2=mrow+1: GO TO %270
 340 IF k$= CHR$ 11 AND mrow>0 THEN mrow2=mrow-1: GO TO %270
 350 IF k$<> CHR$ 13 THEN GO TO %300
 360 IF mcol=1 THEN LET o(mrow+1)=1-o(mrow+1): PRINT AT 2+mrow,18; BRIGHT 1;" X"(o(mrow+1)+1);: GO TO %280
 370 IF mrow=1 THEN PROC Directory(): GO TO %220
 380 IF mrow=2 THEN PROC Help(): GO TO %290
 390 IF mrow=3 THEN RUN AT 0: STOP 
 400 ; 
 410 ; === Main converter ======
 420 ;
 430 PROC FlashLight(1)
 440 CLOSE # 6: OPEN # 6,"u>tapster.txt": DIM #6 TO s: GO TO #6, s
 450 LET bytes=0: LET files=0
 460 LET dupes=o(1): LET fixnames=o(2): LET suffix=o(3): LET headerless=o(4)
 470 PRINT AT 10,2; PAPER 0; INK 7; BRIGHT 1; INT (bytes/1024);"K in ";files;" file(s)     ";
 480 LET q=% DPEEK 23730- DPEEK 23653: IF q < 256 THEN PROC LogPrint("Memory low, only "+ STR$ (q)+" bytes left")
 490 IF headerless THEN DPOKE tape+5,max: ELSE DPOKE tape+5,19
 500 RUN AT 0
 510 LET e= USR tape
 520 RUN AT 3
 530 IF INKEY$ =" " THEN CLOSE # 6: GO TO %230
 540 IF e<2 THEN GO TO %480
 550 IF e>max THEN PROC LogPrint("Too large"): GO TO %480
 560 LPRINT 
 570 DPOKE check+4,e
 580 LET %c= USR check: LET sum =%c & 255
 590 IF sum<>0 THEN PROC LogPrint("Checksum error"): GO TO %480
 600 IF PEEK start=0 AND e=19 THEN GO TO %690
 610 IF PEEK start=0 OR headerless=0 THEN GO TO %470
 620 LET t=5
 630 LET n$="Headerless"
 640 LET l=e-2
 650 LET a=32768
 660 LET b=32768
 670 PROC LogPrint(n$+" (length="+ STR$ l+")")
 680 GO TO %950
 690 LET t= PEEK (start+1)
 700 LET n$= PEEK$ (start+2,10)
 710 LET %s=start
 720 LET l= % DPEEK (s+12)
 730 LET a= % DPEEK (s+14)
 740 LET b= % DPEEK (s+16)
 750 IF LEN (n$)<>0 THEN IF n$( LEN n$)=" " THEN n$=n$( TO LEN n$-1): GO TO %750
 760 IF t=0 THEN LET t$= "Program: ": ELSE IF t=1 THEN LET t$= "Numeric array: ": ELSE IF t=2 THEN LET t$= "Character array: ": ELSE LET t$="Bytes: "
 770 LET t$=t$+n$+" (size "+ STR$ l
 780 IF t=0 AND (a<=9999) THEN LET t$=t$+", line " + STR$ a
 790 IF t=0 AND b<l THEN LET t$=t$+", vars"
 800 IF t=1 OR t=2 THEN LET %n=a: LET nn=%((n/256)&31)+96: LET t$=t$+", name "+ CHR$ nn: IF t=2 THEN LET t$=t$+"$"
 810 IF t=4 THEN LET t$=t$+", addr "+ STR$ a
 820 LET t$=t$+ ")"
 830 PROC LogPrint(t$)
 840 IF l+2>max THEN PROC LogPrint("Too large"): GO TO %480
 850 IF t=5 THEN GO TO %950
 860 DPOKE tape+5,l+2
 870 RUN AT 0
 880 LET e= USR tape
 890 RUN AT 3
 900 IF INKEY$ =" " THEN CLOSE # 6: GO TO %230
 910 IF e-2<l THEN PROC LogPrint("Length error"): GO TO %480
 920 DPOKE check+4,e
 930 LET %c= USR check: LET sum=%c&255
 940 IF sum<>0 THEN PROC LogPrint("Checksum error"): REM GO TO %480
 950 BORDER 7
 960 REM ON ERROR ERROR TO err: PROC LogPrint("Error: "+ STR$ err): ON ERROR : GO TO %460
 970 PROC DiskName(t,n$,l) TO n$
 980 LET z$="Saving as """+n$+"""...": PROC LogPrint(z$)
 985 LET r=0: IF start+1+l>=65536 THEN LET r=start+1+l-65536: LET l=l-r
 987 LPRINT "l=";l;" r=";r
 990 SAVE n$ CODE start+1,l+r
1000 CLOSE # 5
1010 OPEN # 5,"u>"+n$
1015 IF r<>0 THEN GO TO #5,l+128: PROC WriteBytes(5,9,0,r): GO TO #5,0
1020 FOR n=0 TO 127
1030 NEXT #5 TO %x
1040 POKE start+n,%x
1050 NEXT n
1054 REM LET %s=start: LPRINT % DPEEK (s+11)
1055 REM DPOKE start+11,l+r+128
1060 POKE start+15,t
1070 REM DPOKE start+16,l+r
1080 DPOKE start+18,a
1090 DPOKE start+20,b
1100 DPOKE check+4,127: LET %c= USR check: LET %c=%c>>8
1110 POKE start+127,%c
1120 GO TO #5,0
1130 FOR n=0 TO 127
1140 PRINT #5; CHR$ PEEK (start+n);
1150 NEXT n
1160 CLOSE # 5
1170 PROC LogPrint("OK")
1180 LET files=files+1: LET bytes=bytes+l
1190 ON ERROR 
1200 GO TO %470
1210 ;
1220 ; === Draw window =========
1230 ; 
1240 DEFPROC Window(row,col,row2,col2,t$)
1250 LOCAL n,s$,x,y,w,h
1260 DIM s$(col2-col-6- LEN t$)
1270 PRINT AT row,col; PAPER 0; INK 7; BRIGHT 1;" ";t$;s$;
1280 PRINT PAPER 0; INK 2; BRIGHT 1;"";: PRINT PAPER 2; INK 6; BRIGHT 1;"";: PRINT PAPER 6; INK 4; BRIGHT 1;"";: PRINT PAPER 4; INK 5; BRIGHT 1;"";: PRINT PAPER 5; INK 0; BRIGHT 1;"";: PRINT PAPER 0;" ";
1290 DIM s$(col2-col+1)
1300 FOR n=row+1 TO row2: PRINT AT n,col; BRIGHT 1;s$;: NEXT n
1310 LET x=col*8: LET y=176-row*8-8: LET w=(col2-col)*8+7: LET h=(row2-row)*8
1320 PLOT x,y: DRAW 0,-h: DRAW w,0: DRAW 0,h
1330 ENDPROC 
1340 ; 
1350 ; === Show progress =======
1360 ; 
1370 DEFPROC LogPrint(m$)
1380 LOCAL t$
1390 LPRINT m$
1400 PROC DateTime() TO t$
1410 PRINT #6;t$;" ";m$
1420 ENDPROC 
1430 ; 
1440 ; == Get date/time as $ ===
1450 ; 
1460 DEFPROC DateTime()
1470 LOCAL a$,b$
1480 DIM b$(50)
1490 OPEN # 2,"v>b$"
1500 ON ERROR GO TO %1530
1510 .date:.time
1520 LET a$=b$(13 TO 22)+" "+b$(37 TO 44)
1530 CLOSE # 2 
1540 ON ERROR 
1550 ENDPROC = a$
1560 ;
1570 ; === Flashlight on/off ===
1580 ; 
1590 DEFPROC FlashLight(b)
1600 PRINT AT 8,1; PAPER 2; INK 7; BRIGHT b; FLASH b;"  LISTENING - Space to stop.  ";
1610 ENDPROC 
1620 ;
1630 ; === Change directory ====
1640 ; 
1650 DEFPROC Directory()
1660 SAVE "m:tapster.tmp" SCREEN$ 
1670 .browse -k -p "Navigate with cursor keys, ENTER, EDIT and D.      Use K to create new directory and SPACE to confirm." p$
1680 LOAD "m:tapster.tmp" SCREEN$ 
1690 ERASE "m:tapster.tmp"
1700 ENDPROC 
1710 ; 
1720 ; === Show help ===========
1730 ; 
1740 DEFPROC Help()
1750 LPRINT '"This program helps you migrate your tapes to the ZX Spectrum Next's SD card or RAM disk. Select a target directory and you are ready to go. The maximum file size is around 32K (40K when running from a BANK)."''
1760 LPRINT "There are options for handling duplicate files, fixing tape file names that would be invalid on disk, adding proper suffixes (such as .bas or .scr) and allowing headerless files."
1770 ENDPROC 
1780 ;
1790 ; === Fix filename ========
1800 ; 
1810 DEFPROC DiskName(type,n$,size)
1820 LOCAL c$,e$,f$,o$,c,n
1830 IF NOT fixnames THEN LET o$=n$: GO TO %1890
1840 LET o$=""
1850 FOR n=1 TO LEN n$
1860 LET c$=n$(n)
1870 IF c$>="0" AND c$<="9" OR c$>="A" AND c$<="Z" OR c$>="a" AND c$<="z" OR c$="#" OR c$="$" OR c$="@" OR c$="^" OR c$="_" OR c$="{" OR c$="}" OR c$="~" OR c$="`" THEN o$=o$+c$
1880 NEXT n
1890 IF o$="" THEN o$="Untitled"
1900 LET e$=""
1910 IF NOT suffix THEN GO TO %1960
1920 IF type=0 THEN LET e$=".bas": GO TO %1960
1930 IF type=1 THEN LET e$=".num": GO TO %1960
1940 IF type=2 THEN LET e$=".chr": GO TO %1960
1950 IF size=6912 THEN LET e$=".scr": ELSE LET e$=".bin"
1960 IF NOT dupes THEN LET f$=o$+e$: GO TO %2050
1970 ON ERROR GO TO %2040
1980 FOR c=0 TO 9999
1990 LET f$=o$
2000 IF c<>0 THEN LET f$=f$+"-"+ STR$ (c)
2010 LET f$=f$+e$
2020 CLOSE # 5: OPEN # 5,"i>"+f$: CLOSE # 5
2030 NEXT c
2040 ON ERROR 
2050 ENDPROC =f$
2060 ;
2070 ; === Poke MC & UDGs ======
2080 ; 
2090 DEFPROC Pokes()
2100 LOCAL start,max,tape,check,udg
2110 LET start=65000: REM %1+ DPEEK 23730
2120 LET max=49152: REM 65536-start
2130 LET tape=16384
2140 LET check=tape+256
2150 LET udg=check+256
2160 DPOKE 23675,udg
2170 POKE 22528,"???????????????????????????"
2180 POKE USR "a",1,3,7,15,31,63,127,255
2190 POKE tape,221,33,start~,17,0,128,14,14,55,8,243,213,205,108,5,251,225,55,237,82,229,193,3,201
2200 POKE check,33,start~,17,0,0,1,0,0,122,179,200,120,134,71,121,174,79,35,27,24,255-12
2210 ENDPROC =start,max,tape,check
2990 ;
2995 ; === Bytes to stream =====
2997 ;
3000 DEFPROC WriteBytes(stream,bankno,offset,length)
3010 LOCAL chunk,a$
3020 IF length=0 THEN GO TO 3100
3030 IF length>256 THEN chunk=256: ELSE chunk=length
3050 LET a$= BANK bankno PEEK$ (offset, chunk)
3060 PRINT #stream;a$;
3070 LET offset=offset+chunk: LET length=length-chunk
3080 GO TO 3020
3100 ENDPROC 
